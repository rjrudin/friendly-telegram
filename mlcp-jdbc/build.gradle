plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
}

configurations {
    // The h2 database isn't added to testImplementation; this forces tests to use the jdbcDriverPath feature to
    // dynamically load a JDBC driver, which is what users will do as well
    h2
}

dependencies {
    api project(":mlcp-core")

    // Sticking with 5.3.7 as that's the version of Spring that spring-batch:4.3.3 depends on
    api "org.springframework:spring-jdbc:5.3.7"

    testImplementation(testFixtures(project(":mlcp-core")))

    h2 "com.h2database:h2:1.4.193"
}

test {
    useJUnitPlatform()
}

task setupH2Database(type: JavaExec) {
    description = "Sets up the test H2 database at ..data/h2/h2-sample-db.mv.db; that file is version-controlled though " +
        "so this only needs to be run if you update the setup script"
    classpath = configurations.h2
    mainClass = "org.h2.tools.RunScript"
    args = [
        "-url", "jdbc:h2:file:../data/h2/h2-sample-db",
        "-user", "sa",
        "-script", "../data/h2/setup-h2sample-db.sql"
    ]
}
